import json

import pandas as pd

from utils import genomics, io, pheniqs

configfile: "config.yaml"

CONTEXT = ["CG", "CHG", "CHH"]
MREGION = ["HMR", "cHMR"]

units, samples, readgroups = io.get_multiplex_data(config["data"])

rule _output:
    input:
        expand("{unit}_demux_config.json",
               unit=units.index.values),
        expand("{unit}_demux.cram",
               unit=units.index.values),
        expand("{unit}_demux_report.json",
               unit=units.index.values),
        expand("{readgroup_prefix}_adapter_metrics.txt",
               readgroup_prefix=readgroups.prefix.values),
        expand("{readgroup_prefix}_mapped.bam",
               readgroup_prefix=readgroups.prefix.values),
        expand("{readgroup_prefix}_mapped.bam.bai",
               readgroup_prefix=readgroups.prefix.values),
        expand("{readgroup_prefix}_mapped.{stats}",
               readgroup_prefix=readgroups.prefix.values,
               stats=["stats", "flagstats", "idxstats"]),
        expand("{readgroup_prefix}.vcf.{suffix}",
               readgroup_prefix=readgroups.prefix.values,
               suffix=["gz", "gz.tbi"]),
        expand("{readgroup_prefix}_smp.bed.{suffix}",
               readgroup_prefix=readgroups.prefix.values,
               suffix=["gz", "gz.tbi"]),
        expand("{readgroup_prefix}_snp.bed.{suffix}",
               readgroup_prefix=readgroups.prefix.values,
               suffix=["gz", "gz.tbi"]),
        expand("{readgroup_prefix}_{context}-meth.bw",
               readgroup_prefix=readgroups.prefix.values,
               context=CONTEXT),
        expand("{readgroup_prefix}_{context}.meth",
               readgroup_prefix=readgroups.prefix.values,
               context=CONTEXT),
        expand("{readgroup_prefix}_{context}_{mregion}.{suffix}",
               readgroup_prefix=readgroups.prefix.values,
               context=CONTEXT,
               mregion=MREGION,
               suffix=["bed", "bedgraph"]),
        expand("proportion_table_{context}.txt",
               context=CONTEXT),

include: "rules/pheniqs_interleave.smk"
include: "rules/pheniqs_demultiplex_config.smk"
include: "rules/pheniqs_demultiplex.smk"
include: "rules/view_readgroup_ubam.smk"
include: "rules/picard_markadapt.smk"
include: "rules/picard_samtofastq.smk"
include: "rules/biscuit_align.smk"
include: "rules/picard_sort.smk"
include: "rules/picard_reorder.smk" # maybe avoidable this using samtools faidx TAIR10.fasta 1 2 3 4 5 Mt Pt > TAIR10.reordered.fasta
include: "rules/picard_mergebam.smk"
include: "rules/biscuit_markdup.smk"
include: "rules/picard_sort_markdup.smk"
include: "rules/samtools_index.smk"
include: "rules/samtools_statistics.smk"
include: "rules/biscuit_pileup.smk"
include: "rules/biscuit_vcf2bed_smp.smk"
include: "rules/biscuit_vcf2bed_snp.smk"
include: "rules/methylation_bigwig.smk"
include: "rules/smp_to_methcounts.smk"
include: "rules/methpipe_hmr.smk"
include: "rules/merge_methcounts.smk"
